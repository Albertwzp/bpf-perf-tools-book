#!/usr/local/bin/bpftrace
/*
 * qdisctxlat - Show qdisc transmit queue latency.
 *
 * See BPF Performance Tools, Chapter 10, for an explanation of this tool.
 *
 * Copyright 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-iiiii
 * When copying or porting, include this comment.
 */

#include <linux/skbuff.h>

BEGIN
{
	printf("Tracing qdisc xmit latency. Hit Ctrl-C to end.\n");
}

tracepoint:net:net_dev_queue
{
	@start[args->skbaddr] = nsecs;
}

kprobe:skb_segment    { @segment[tid] = @start[arg0]; }
kretprobe:skb_segment { @segment[tid] = 0; }
kretprobe:__alloc_skb
/@segment[tid]/
{
	// for GSO, copy timestamps to each segmented skb
	@start[retval] = @segment[tid];
}

tracepoint:net:net_dev_start_xmit
/@start[args->skbaddr]/
{
	@us = hist((nsecs - @start[args->skbaddr]) / 1000);
	delete(@start[args->skbaddr]);
}

END
{
	clear(@start);
	clear(@segment);
}
